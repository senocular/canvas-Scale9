<!doctype html>
<html>
<head>
	<meta charset="utf-8">
	<title>Canvas</title>
	<style type="text/css">
	#canvas {
		border:1px dashed #666;
		cursor: pointer;
	}
	</style>
	<script type="text/javascript" src="http://cdnjs.cloudflare.com/ajax/libs/coffee-script/1.1.2/coffee-script.min.js"></script>
</head>
<body>

<h3>Click and drag to resize</h3>
<canvas id="canvas" width="900" height="600" style=""></canvas>

<script type="text/coffeescript" src="../src/scale9.coffee"></script>
<script type="text/coffeescript">
class Example

	constructor: ->
		@img = new Image
		@img.onload = @assetsLoadedHandler
		@context = (document.getElementById "canvas").getContext "2d"

	init: (src) ->
		@img.src = "TheWineGlass.jpg"  # calls setup when loaded
		@

	setup: ->
		@scaler = new Scale9 @img, 60, 60, @img.width-120, @img.height-120 
		@draw @img.width, @img.height

		@context.canvas.addEventListener "mousedown", @enableResizeImageHandler
		document.addEventListener "mouseup", @disableResizeImageHandler

	draw: (w, h) ->
		@context.clearRect 0, 0, @context.canvas.width, @context.canvas.height
		@context.globalAlpha = 0.25
		@context.drawImage @img, 0, 0 

		@context.globalAlpha = 1.0
		@scaler.resize Math.ceil(w), Math.ceil(h)
		@scaler.drawImageTo @context, 0, 0 


	# event handlers

	assetsLoadedHandler: (event) =>
		@setup()

	resizeImageHandler: (event) =>

		# resize based on mouse position (position is size)

		elem = @context.canvas
		rect = elem.getBoundingClientRect()
		mouseX = event.clientX + elem.scrollLeft - elem.clientLeft - rect.left
		mouseY = event.clientY + elem.scrollTop - elem.clientTop - rect.top

		@draw mouseX, mouseY

	enableResizeImageHandler: (event) =>
		document.addEventListener "mousemove", @resizeImageHandler
		event.preventDefault();

	disableResizeImageHandler: (event) =>
		document.removeEventListener "mousemove", @resizeImageHandler


ex = new Example().init()

</script>
</body>
</html>